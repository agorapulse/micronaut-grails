/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020 Vladimir Orany.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.groovy-project'
    id 'io.github.gradle-nexus.publish-plugin'
}

if (!project.hasProperty('ossrhUsername'))      ext.ossrhUsername       = System.getenv('SONATYPE_USERNAME') ?: '**UNDEFINED**'
if (!project.hasProperty('ossrhPassword'))      ext.ossrhPassword       = System.getenv('SONATYPE_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingKeyId'))       ext.signingKeyId        = System.getenv('SIGNING_KEY_ID') ?: '**UNDEFINED**'
if (!project.hasProperty('signingPassword'))    ext.signingPassword     = System.getenv('SIGNING_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingSecretKey'))   ext.signingSecretKey    = System.getenv('SIGNING_SECRET_KEY_PATH') ? rootProject.file(System.getenv('SIGNING_SECRET_KEY_PATH')).text : '**UNDEFINED**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'Micronaut Grails'
        vendor      = 'Agorapulse'
        description = 'Grails Integration for Micronaut'

        links {
            website      = "https://github.com/${slug}"
            issueTracker = "https://github.com/${slug}/issues"
            scm          = "https://github.com/${slug}.git"
        }

        people {
            person {
                id    = 'musketyr'
                name  = 'Vladimir Orany'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    publishing {
        enabled = false
        signing {
            enabled =  true
            keyId = signingKeyId
            secretKey = signingSecretKey
            password = signingPassword
        }
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }

    docs {
        groovydoc {
            enabled = false
            aggregate {
                enabled = false
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}


allprojects {
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/artifactory/core" }
        maven { url "https://repo.spring.io/release"  }
    }

    license {
        exclude '**/*.json'
        exclude '**/*.groovy.txt'
        exclude '***.yml'
    }
}

subprojects { Project subproject ->
    if (subproject.name == 'guide') return

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'codenarc'
    apply plugin: 'micronaut-compatibility'

    configurations {
        optional.extendsFrom compile

        compileOnly.extendsFrom optional
        testCompile.extendsFrom optional
    }

    dependencyManagement {
        imports {
            mavenBom "io.micronaut:micronaut-bom:$micronautVersion"
        }
    }

    dependencies {
        annotationProcessor "io.micronaut:micronaut-inject-java"

        // Groovy
        compile "org.codehaus.groovy:groovy:$groovyVersion"

        compile micronautMigratedDependency('micronaut-runtime-groovy')
        compile "io.micronaut:micronaut-inject"
        compile "io.micronaut:micronaut-runtime"

        compileOnly "io.micronaut:micronaut-inject-groovy"
        compileOnly "io.micronaut:micronaut-inject-java"


        runtime "ch.qos.logback:logback-classic:1.2.3"

        testAnnotationProcessor "io.micronaut:micronaut-inject-java"

        testCompile "io.micronaut:micronaut-inject-groovy"


        testCompile platform("org.junit:junit-bom:$junitVersion")
        testCompile "org.junit.jupiter:junit-jupiter-engine"
        testCompile "org.junit.jupiter:junit-jupiter-api"

        testCompile("org.spockframework:spock-core:$spockVersion")
        testCompile 'cglib:cglib-nodep:3.2.0'
        testCompile 'org.objenesis:objenesis:1.2'

        testCompileOnly 'com.agorapulse:remember:0.1'
    }

    compileJava.options.compilerArgs += '-parameters'
    compileTestJava.options.compilerArgs += '-parameters'

    test {
        useJUnitPlatform()
        environment 'AWS_CBOR_DISABLE', 'true'
        systemProperty 'TEST_RESOURCES_FOLDER', new File(subproject.projectDir, 'src/test/resources').canonicalPath
        systemProperty 'user.timezone', 'UTC'
        systemProperty 'user.language', 'en'
    }

    task cleanOut(type: Delete) {
        delete file('out')
    }

    clean.dependsOn cleanOut

    checkstyle {
        toolVersion = '8.27'
    }

    codenarc {
        toolVersion = '2.1.0'
    }
}
